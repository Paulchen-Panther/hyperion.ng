set(USE_SYSTEM_QMDNS_LIBS ${DEFAULT_USE_SYSTEM_QMDNS_LIBS} CACHE BOOL "use qmdnsengine library from system")

if(USE_SYSTEM_QMDNS_LIBS)
	find_package(qmdnsengine REQUIRED)
else()
	set(QMDNS_CMAKE_ARGS
		-DBUILD_SHARED_LIBS:BOOL=OFF
		-DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_BINARY_DIR}
		-DBIN_INSTALL_DIR:STRING=lib
		-DCMAKE_PREFIX_PATH:PATH=${CMAKE_PREFIX_PATH}
		-DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
		-DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
		-DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
		-DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
		-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
		-Wno-dev # We don't want to be warned over unused variables
	)

	if(CMAKE_OSX_ARCHITECTURES)
		list(APPEND QMDNS_CMAKE_ARGS
			-DCMAKE_OSX_ARCHITECTURES:STRING=${CMAKE_OSX_ARCHITECTURES}
			-DCMAKE_OSX_DEPLOYMENT_TARGET:STRING=${CMAKE_OSX_DEPLOYMENT_TARGET}
			-DCMAKE_OSX_SYSROOT:PATH=${CMAKE_OSX_SYSROOT}
		)
	endif()

	include(ExternalProject)
	ExternalProject_Add(qmdns
		PREFIX				${CMAKE_BINARY_DIR}/dependencies/external/qmdnsengine
		BUILD_ALWAYS		OFF
		DOWNLOAD_COMMAND	""
		SOURCE_DIR			${CMAKE_SOURCE_DIR}/dependencies/external/qmdnsengine
		CMAKE_ARGS			${QMDNS_CMAKE_ARGS}
		INSTALL_DIR			${CMAKE_BINARY_DIR}
		BUILD_BYPRODUCTS    <INSTALL_DIR>/lib/${CMAKE_STATIC_LIBRARY_PREFIX}qmdnsengine${CMAKE_STATIC_LIBRARY_SUFFIX}
	)

	add_library(qmdnsengine STATIC IMPORTED GLOBAL)
	ExternalProject_Get_Property(qmdns INSTALL_DIR)
	set_target_properties(qmdnsengine PROPERTIES
		IMPORTED_LOCATION ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}qmdnsengine${CMAKE_STATIC_LIBRARY_SUFFIX}
		INTERFACE_INCLUDE_DIRECTORIES ${CMAKE_BINARY_DIR}/include
	)

	add_dependencies(qmdnsengine qmdns)
endif()
